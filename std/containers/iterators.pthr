////////////////////////////////////////////////////////////////////////////////////
//                                                                                //
// Part of Panther-STD, under the BSD 1-Clause License with PCIT Exceptions.      //
// You may not use this file except in compliance with the License.               //
// See `https://github.com/PCIT-Project/Panther-std/blob/main/LICENSE`for info.   //
//                                                                                //
////////////////////////////////////////////////////////////////////////////////////


def std = @import("std");



type ArrayRefIter = struct <{ARRAY_REF_TYPE: Type}> #pub {
	type ELEM_TYPE = alias type(@arrayRefElementTypeID<{ARRAY_REF_TYPE}>());

	func new = (arr_ref_ptr: ARRAY_REF_TYPE) -> (output: This) {
		output.current = arr_ref_ptr.data();
		output.last = arr_ref_ptr[arr_ref_ptr.size() - 1];
		return...;
	}

	func next = (this mut) -> Void {
		unsafe{
			const new_address: USize = std.bit.bitCast<{USize}>(this.current) + @numBytes<{ELEM_TYPE, true}>();
			this.current = std.bit.bitCast<{ELEM_TYPE*}>(new_address);
		}
	}

	func get = (this) -> ELEM_TYPE* { return copy this.current; }

	func atEnd = (this) -> Bool {
		unsafe{
			return std.bit.bitCast<{USize}>(this.current) > std.bit.bitCast<{USize}>(this.last);
		}
	}


	impl @pthr.IIterator {
		next  = next,
		get   = get,
		atEnd = atEnd,
	}

	impl @pthr.IIteratorRT {
		next  = next,
		get   = get,
		atEnd = atEnd,
	}

	var current: ELEM_TYPE*;
	var last: ELEM_TYPE*;
}



type MutArrayRefIter = struct <{ARRAY_REF_TYPE: Type}> #pub {
	type ELEM_TYPE = alias type(@arrayRefElementTypeID<{ARRAY_REF_TYPE}>());

	func new = (arr_ref_ptr: ARRAY_REF_TYPE) -> (output: This) {
		output.current = arr_ref_ptr.data();
		output.last = arr_ref_ptr[arr_ref_ptr.size() - 1];
		return...;
	}

	func next = (this mut) -> Void {
		unsafe{
			const new_address: USize = std.bit.bitCast<{USize}>(this.current) + @numBytes<{ELEM_TYPE, true}>();
			this.current = std.bit.bitCast<{ELEM_TYPE*mut}>(new_address);
		}
	}

	func get = (this) -> ELEM_TYPE*mut { return copy this.current; }

	func atEnd = (this) -> Bool {
		unsafe{
			return std.bit.bitCast<{USize}>(this.current) > std.bit.bitCast<{USize}>(this.last);
		}
	}


	impl @pthr.IMutIterator {
		next  = next,
		get   = get,
		atEnd = atEnd,
	}

	impl @pthr.IMutIteratorRT {
		next  = next,
		get   = get,
		atEnd = atEnd,
	}

	var current: ELEM_TYPE*mut;
	var last: ELEM_TYPE*mut;
}
