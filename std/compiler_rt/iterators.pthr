

//////////////////////////////////////////////////////////////////////
// array ref iter


type ArrayRefIter = struct <{ARRAY_REF_TYPE: Type}> {
	type ELEM_TYPE = type(@arrayRefElementTypeID<{ARRAY_REF_TYPE}>());

	func new = (arr_ref_ptr: ARRAY_REF_TYPE) -> (output: This) {
		output.current = arr_ref_ptr.data();
		output.last = arr_ref_ptr[arr_ref_ptr.size() - 1];
		return...;
	}

	func next = (this mut) -> Void {
		const new_address: USize = @bitCast<{ELEM_TYPE*, USize}>(this.current) + @numBytes<{ELEM_TYPE, true}>();
		this.current = @bitCast<{USize, ELEM_TYPE*}>(new_address);
	}

	func get = (this) -> ELEM_TYPE* { return copy this.current; }

	func atEnd = (this) -> Bool {
		return @bitCast<{ELEM_TYPE*, USize}>(this.current) > @bitCast<{ELEM_TYPE*, USize}>(this.last);
	}


	impl @pthr.IIterator {
		next  = next,
		get   = get,
		atEnd = atEnd,
	}

	impl @pthr.IIteratorRT {
		next  = next,
		get   = get,
		atEnd = atEnd,
	}

	var current: ELEM_TYPE*;
	var last: ELEM_TYPE*;
}


type MutArrayRefIter = struct <{ARRAY_REF_TYPE: Type}> {
	type ELEM_TYPE = type(@arrayRefElementTypeID<{ARRAY_REF_TYPE}>());

	func new = (arr_ref_ptr: ARRAY_REF_TYPE) -> (output: This) {
		output.current = arr_ref_ptr.data();
		output.last = arr_ref_ptr[arr_ref_ptr.size() - 1];
		return...;
	}

	func next = (this mut) -> Void {
		const new_address: USize = @bitCast<{ELEM_TYPE*mut, USize}>(this.current) + @numBytes<{ELEM_TYPE, true}>();
		this.current = @bitCast<{USize, ELEM_TYPE*mut}>(new_address);
	}

	func get = (this) -> ELEM_TYPE*mut { return copy this.current; }

	func atEnd = (this) -> Bool {
		return @bitCast<{ELEM_TYPE*mut, USize}>(this.current) > @bitCast<{ELEM_TYPE*mut, USize}>(this.last);
	}


	impl @pthr.IMutIterator {
		next  = next,
		get   = get,
		atEnd = atEnd,
	}

	impl @pthr.IMutIteratorRT {
		next  = next,
		get   = get,
		atEnd = atEnd,
	}

	var current: ELEM_TYPE*mut;
	var last: ELEM_TYPE*mut;
}



impl [$$:*] #builtin("arrayRef.IIterableRef") {
	createIterator = (this) -> impl(ArrayRefIter<{This}>:@pthr.IIterator) {
		return new impl(ArrayRefIter<{This}>:@pthr.IIterator)(this);
	},
}

impl [$$:*] #builtin("arrayRef.IIterableRefRT") {
	createIterator = (this) -> impl(ArrayRefIter<{This}>:@pthr.IIterator) {
		return new impl(ArrayRefIter<{This}>:@pthr.IIterator)(this);
	},
}


impl [$$:*mut] #builtin("arrayMutRef.IIterableMutRef") {
	createIterator = (this) -> impl(MutArrayRefIter<{This}>:@pthr.IMutIterator) {
		return new impl(MutArrayRefIter<{This}>:@pthr.IMutIterator)(this);
	},
}

impl [$$:*mut] #builtin("arrayMutRef.IIterableMutRefRT") {
	createIterator = (this) -> impl(MutArrayRefIter<{This}>:@pthr.IMutIterator) {
		return new impl(MutArrayRefIter<{This}>:@pthr.IMutIterator)(this);
	},
}




//////////////////////////////////////////////////////////////////////
// array iter


impl [$$:$$] #builtin("array.IIterable") {
	createIterator = (this) -> impl(ArrayRefIter<{[type(@arrayElementTypeID<{This}>()):*]}>:@pthr.IIterator) {
		return new impl(ArrayRefIter<{[type(@arrayElementTypeID<{This}>()):*]}>:@pthr.IIterator)(
			this as [type(@arrayElementTypeID<{This}>()):*]
		);
	},

	createIterator = (this mut)
	-> impl(MutArrayRefIter<{[type(@arrayElementTypeID<{This}>()):*mut]}>:@pthr.IMutIterator) {
		return new impl(MutArrayRefIter<{[type(@arrayElementTypeID<{This}>()):*mut]}>:@pthr.IMutIterator)(
			this as [type(@arrayElementTypeID<{This}>()):*mut]
		);
	},
}


impl [$$:$$] #builtin("array.IIterableRT") {
	createIterator = (this) -> impl(ArrayRefIter<{[type(@arrayElementTypeID<{This}>()):*]}>:@pthr.IIterator) {
		return new impl(ArrayRefIter<{[type(@arrayElementTypeID<{This}>()):*]}>:@pthr.IIterator)(
			this as [type(@arrayElementTypeID<{This}>()):*]
		);
	},

	createIterator = (this mut)
	-> impl(MutArrayRefIter<{[type(@arrayElementTypeID<{This}>()):*mut]}>:@pthr.IMutIterator) {
		return new impl(MutArrayRefIter<{[type(@arrayElementTypeID<{This}>()):*mut]}>:@pthr.IMutIterator)(
			this as [type(@arrayElementTypeID<{This}>()):*mut]
		);
	},
}
